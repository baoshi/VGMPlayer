; Modified from Raspberry Pi's "pico-extra" i2s pio example

; This is 16 bits per sample
;
; Autopull must be enabled, with threshold set to 32.
; Since I2S is MSB-first, shift direction should be to left.
; Hence the format of the FIFO word is:
;
; | 31   :   16 | 15   :    0 |
; | left  ws=0  | right  ws=1 |
;
; Data is output at 1 bit per clock. Use clock divider to adjust frequency.
; Fractional divider will probably be needed to get correct bit clock period,
; but for common syslck freqs this should still give a constant word select period.
;
; One output pin is used for the data output.
; Two side-set pins are used. Bit 0 is clock, bit 1 is word select.

.program pio_i2s
.side_set 2
                    ;        /--- LRC(WS)
                    ;        |/-- BCLK
bitloop1:           ;        ||
    out pins, 1       side 0b00     ;   bits 31 : 17 
    jmp x-- bitloop1  side 0b01     ;
    out pins, 1       side 0b10     ;   bit 16. LRC =1 for next word
    mov x, y          side 0b11     ;   reset counter to 14
bitloop0:
    out pins, 1       side 0b10     ;   bits 16 : 1
    jmp x-- bitloop0  side 0b11
    out pins, 1       side 0b00     ;   bit 0. LRC = 0 for for next word
public entry_point:
    mov x, y          side 0b01     ;   y initially set to 14
